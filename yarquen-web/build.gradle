import com.sun.java.swing.plaf.gtk.GTKFileChooserUI.RenameFileAction;

import groovy.sql.ExpandedVariable;

/**
 * yarquen-web build script
 *
 * author: Jorge Riquelme
 * date: 26/11/2012
 * version: $Id$
 */

apply plugin: "war"
apply plugin: "eclipse-wtp"

if (hasProperty("production")) {
	jar.exclude "rebel.xml"
}

ext {
	thymeleafVersion = "2.0.16"
	thymeleafSpringVersion = "2.0.16"
	thymeleafSpringSecurityVersion = "2.0.0"
	luceneVersion = "4.1.0"
	springSecurity = "3.1.3.RELEASE"
}

dependencies {
	providedCompile "javax.servlet:javax.servlet-api:3.0.1"

	compile project(":yarquen-common")	
	compile "org.springframework:spring-webmvc:$springVersion"
	compile "org.springframework:spring-web:$springVersion"
	compile "org.springframework.security:spring-security-config:$springSecurity"
	compile ("org.springframework.security:spring-security-web:$springSecurity") {
		exclude group: "org.springframework", module: "spring-jdbc"
	}
	compile ("org.springframework.security:spring-security-taglibs:$springSecurity") {
		exclude group: "org.springframework", module: "spring-jdbc"
	}
	compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity3:$thymeleafSpringSecurityVersion"
	compile "org.thymeleaf:thymeleaf:$thymeleafVersion"
	compile "org.thymeleaf:thymeleaf-spring3:$thymeleafSpringVersion"
	
	compile "org.apache.lucene:lucene-core:$luceneVersion"
	compile "org.apache.lucene:lucene-analyzers-common:$luceneVersion"
	compile "org.apache.lucene:lucene-queryparser:$luceneVersion"
	compile "org.apache.lucene:lucene-facet:$luceneVersion"
	compile "commons-io:commons-io:2.4"
	
	runtime "ch.qos.logback:logback-classic:1.0.9"
}


//distribution
task unpackTomcat(type: Copy) {
	from tarTree(tomcatPath)
	into "$buildDir"
	exclude "**/webapps/docs/**"
	exclude "**/webapps/examples/**"
	exclude "**/webapps/host-manager/**"
	exclude "**/webapps/manager/**"
}

task copyDistBin(type: Copy, dependsOn: unpackTomcat) {
	from "src/main/distribution/bin"
	into "$buildDir/$tomcatDir/bin"
}
task copyDistLogback(type: Copy, dependsOn: unpackTomcat) {
	from "src/main/distribution/conf/logback.xml"
	into "$buildDir/$tomcatDir/conf"
}
task copyDistYarquenProps(type: Copy, dependsOn: unpackTomcat) {
	from "src/main/distribution/conf/yarquen.properties"
	into "$buildDir/$tomcatDir/conf"
}
task copyDistServer(type: Copy, dependsOn: unpackTomcat) {
	from "src/main/distribution/conf/server.xml"
	into "$buildDir/$tomcatDir/conf"
	expand (shutdownPort: shutdownPort, connectorPort: connectorPort, redirectPort: redirectPort, ajpPort: ajpPort)
}
task copyWar(type: Copy, dependsOn: [ unpackTomcat, war ]) {
	from war.archivePath
	into "$buildDir/$tomcatDir/webapps"
	rename(war.archiveName, "yarquen.war")
}

task dist(type: Tar, dependsOn: [copyDistBin, copyDistLogback, copyDistYarquenProps, copyDistServer, copyWar]) {
	compression = Compression.GZIP
	from "$buildDir/$tomcatDir"
}


